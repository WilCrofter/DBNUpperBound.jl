{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Bounds on the Tail of Φ\n",
    "\n",
    "[Comment on the PM 15 5<sup>th</sup> thread](https://terrytao.wordpress.com/2018/03/02/polymath15-fifth-thread-finishing-off-the-test-problem/#comment-493554) (Terrence Tao,)\n",
    "\n",
    "\"I don’t know if anyone has explicitly computed bounds for the tail of $\\Phi$ here, but given the rapid decay, perhaps even very crude bounds would be sufficient, as long as $\\mathrm{Im}(u)$ stays a little bit away from $\\pm \\pi/8$, say $\\mathrm{Im}(u) \\leq \\frac{\\pi}{8}-\\varepsilon$ so that $e^{4u}$ has some substantial real part in the sense that its argument is between $-\\frac{\\pi}{4}+4\\varepsilon$ and $\\frac{\\pi}{4} - 4\\varepsilon$, so that $|e^{4u}| \\leq \\mathrm{cosec(4\\varepsilon)} \\mathrm{Re} e^{4u}$. If for $\\theta = 5/4, 9/4$ one lets $C_\\theta$ be a constant for which $x^\\theta \\leq C \\exp(x)$ for all ~~C>0~~ $x>0$, then\n",
    "\n",
    "$$2\\pi^2 n^4 |e^{9u}| \\leq \\mathrm{cosec}(4\\varepsilon)^{9/4} 2\\pi^2 (\\pi/2)^{-9/4} (\\pi n^2 \\mathrm{Re}(e^{4u})/2)^{9/4} \\leq \\mathrm{cosec}(4\\varepsilon)^{9/4} 2\\pi^2 (\\pi/2)^{-9/4} C_{9/4} \\exp( \\pi n^2 \\mathrm{Re}(e^{4u})/2)$$\n",
    "\n",
    "\"and similarly for $3\\pi n^2 |e^{5u}|$, which should allow one to bound each summand in $\\Phi$ by an explicit multiple of $\\exp( -\\pi n^2 \\mathrm{Re} e^{4u} /2)$ in magnitude. One can then control the tail of such a sum $\\sum_{n \\geq n_0} \\exp( -\\pi n^2 \\mathrm{Re} e^{4u} /2)$ by for instance comparing it with a geometric series (e.g. replacing $n^2$ with $n n_0$). Presumably these sorts of bounds can also then be used to control the tail of $H_t$.\"\n",
    "\n",
    "[Also](https://terrytao.wordpress.com/2018/03/02/polymath15-fifth-thread-finishing-off-the-test-problem/#comment-493623)\n",
    "\n",
    "\"Another slightly different way to proceed is just to bound $n^2$ by $n\\times n_0$ from the beginning and use the generalised geometric series formulae to evaluate expressions such as $\\sum_{n \\geq n_0} 2\\pi^2 n^4 \\exp( - \\pi n n_0 \\mathrm{Re} e^{4u})$ explicitly.\"\n",
    "\n",
    "With respect to the latter approach, in `notebooks/summation_formulae.ipynb` summation formulae and associated Python code were autogenerated using [SymPy](http://www.sympy.org/en/index.html). Here, the Python code is ported to Julia and incorporated in bounds.\n",
    "\n",
    "Since $\\sum_{n \\ge n_0}e^{-nx}= \\frac{e^{-n_0x}}{1-e^{-x}}$, it follows that \n",
    "\n",
    "$$\\sum_{n \\ge n_0}n^2e^{-nx}= \\frac{d^2}{dx^2}\\frac{e^{-n_0x}}{1-e^{-x}} = \\frac{e^{x \\left(2 n_{0} + 1\\right)}}{\\left(e^{x} - 1\\right)^{3}} \\left(n_{0}^{2} \\left(e^{x} - 1\\right)^{2} + \\left(2 n_{0} + 1\\right) \\left(e^{x} - 1\\right) + 2\\right) e^{- 3 n_{0} x} \\quad (I)$$\n",
    "\n",
    "Modifying the associated Python code slightly and checking against a Python result,"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "D2(2.0, 5) = 0.0014056170035180444\n"
     ]
    }
   ],
   "source": [
    "function D2(x::Real, n0::Int)\n",
    "    return n0^2*exp(-n0*x)/(1 - exp(-x)) + 2*n0*exp(-x)*exp(-n0*x)/\n",
    "    (1 - exp(-x))^2 + exp(-x)*exp(-n0*x)/(1 - exp(-x))^2 + 2*exp(-2*x)*exp(-n0*x)/(1 - exp(-x))^3\n",
    "end\n",
    "\n",
    "@show D2(2.0, 5) # compare with Python result 0.00140561700351804\n",
    "nothing"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Similarly,\n",
    "\n",
    "$$\\sum_{n \\ge n_0}n^4e^{-nx}= \\frac{d^4}{dx^4}\\frac{e^{-n_0x}}{1-e^{-x}} = \\frac{e^{x \\left(4 n_{0} + 1\\right)}}{\\left(e^{x} - 1\\right)^{5}} \\left(n_{0}^{4} \\left(e^{x} - 1\\right)^{4} + 12 \\left(2 n_{0} + 3\\right) \\left(e^{x} - 1\\right) + \\left(e^{x} - 1\\right)^{3} \\left(4 n_{0}^{3} + 6 n_{0}^{2} + 4 n_{0} + 1\\right) + 2 \\left(e^{x} - 1\\right)^{2} \\left(6 n_{0}^{2} + 12 n_{0} + 7\\right) + 24\\right) e^{- 5 n_{0} x} \\quad (II)$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "D4(2.0, 5) = 0.03892088036985503\n"
     ]
    }
   ],
   "source": [
    "function D4(x::Real, n0::Int)\n",
    "    return (n0^4*(exp(x) - 1)^4 + 12*(2*n0 + 3)*(exp(x) - 1) + (exp(x) - 1)^3*(4*n0^3 + 6*n0^2 + 4*n0 + 1) + \n",
    "        2*(exp(x) - 1)^2*(6*n0^2 + 12*n0 + 7) + 24)*exp(-5*n0*x)*exp(x*(4*n0 + 1))/(exp(x) - 1)^5\n",
    "end\n",
    "\n",
    "@show D4(2.0, 5) # compare with Python result 0.038920880369855\n",
    "nothing"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Recall that\n",
    "\n",
    "$$\\Phi(u) := \\sum_{n=1}^\\infty (2\\pi^2 n^4 e^{9u} - 3 \\pi n^2 e^{5u}) \\exp(-\\pi n^2 e^{4u})$$\n",
    "\n",
    "Since $\\Phi(u) = \\Phi(-u)$ it suffices to assume $u \\ge 0$. Hence $\\forall u \\ge 0$ and $n \\ge 1,$\n",
    "\n",
    "$2\\pi n^2 e^{4u} > 3 \\Rightarrow 2\\pi^2n^4e^{9u} > 3\\pi n^2 e^{5u} \\Rightarrow 2\\pi^2n^4e^{9u} - 3\\pi n^2 e^{5u} > 0.$\n",
    "\n",
    "Hence,\n",
    "\n",
    "$\\sum_{n=n_0}^\\infty (2\\pi^2 n^4 e^{9u} - 3 \\pi n^2 e^{5u}) \\exp(-\\pi n^2 e^{4u}) $\n",
    "\n",
    "$\\le \\sum_{n=n_0}^\\infty (2\\pi^2 n^4 e^{9u} - 3 \\pi n^2 e^{5u}) \\exp(-\\pi n_0 n e^{4u})$\n",
    "\n",
    "$ = \\sum_{n=n_0}^\\infty 2\\pi^2 n^4 e^{9u} \\exp(-\\pi n_0 n e^{4u}) - \\sum_{n=n_0}^\\infty 3 \\pi n^2 e^{5u} \\exp(-\\pi n_0 n e^{4u})$\n",
    "\n",
    "$ = 2\\pi^2 e^{9u} \\sum_{n=n_0}^\\infty n^4 \\exp(- n \\pi n_0 e^{4u}) - 3 \\pi e^{5u} \\sum_{n=n_0}^\\infty  n^2 \\exp(-n \\pi n_0 e^{4u})$\n",
    "\n",
    "$ =  2\\pi^2 e^{9u} (\\frac{d^4}{dx^4} \\frac{e^{-n_0x}}{1-e^{-x}})\\big \\vert_{x=\\pi n_0 e^{4u}} -  3 \\pi e^{5u}(\\frac{d^2}{dx^2} \\frac{e^{-n_0x}}{1-e^{-x}})\\big \\vert_{x=\\pi n_0 e^{4u}}$\n",
    "\n",
    "Theoretically, the bound could be coded directly from the above expression using the essentially autogenerated functions, `D4` and `D2`, above. However the autogenerated code is not very good numerically. As written the exponentials will produce $\\infty$'s, $0$'s, and products thereof, but it can serve as a check on manually written code."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "After multiplying factors through the sums, exponential expressions in the individual terms of (I) and (II) have the general form:\n",
    "\n",
    "$$\\frac{e^{x(1-n_0)}}{(e^x-1)^k} = e^{-x(n_0-1)} \\left(\\frac{e^{-x}}{1-e^{-x}} \\right)^k$$ \n",
    "\n",
    "where $1 \\le k \\le 4$, $x=\\pi n_0 e^{4u}$ and $u$ may be any non-negative real. (Attention here is restricted to real $u$.) The factor $e^{-x(n_0-1)}$ is common to all terms in both summations and may be ignored for now. Since $x \\ge \\pi n_0$ is fairly large, the ratio $\\frac{e^{-x}}{1-e^{-x}}$ is well-behaved numerically. Equation (I) may be rewritten,\n",
    "\n",
    "$$ \\sum_{n \\ge n_0}n^2e^{-nx} = e^{-x(n_0-1)}\\left( n_0^2y + (2n_0+1)y^2 + 2y^3 \\right) \\quad (Ia)$$\n",
    "\n",
    "where $y = \\frac{e^{-x}}{1-e^{-x}}$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "D2a(2.0, 5) = 0.0014056170035180449\n",
      "D2a(1.0e6, 10) = 1.516936780898734357589463782261016246401085581490856436959012937511728895412703e-4342943\n",
      "D2a(1.0e6, 100) = 6.451709692821766008843654827135633239697947425281381499819553501855330737159919e-43429445\n"
     ]
    }
   ],
   "source": [
    "using DBNUpperBound\n",
    "using DBNUpperBound.Asymptotics\n",
    "\n",
    "function D2a(x::Real, n0::Int)\n",
    "    y = bigexp(-x)/(1-bigexp(-x))   \n",
    "    return real(bigexp(-x*(n0-1))*y*(n0^2 + y*(2*n0+1 + 2*y)))\n",
    "end\n",
    "\n",
    "@show D2a(2.0,5) # compare with 0.00140561700351804\n",
    "@show D2a(1e6,10)\n",
    "@show D2a(1e6,100)\n",
    "nothing"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Similarly, equation (II) may be rewritten\n",
    "\n",
    "$\\sum_{n \\ge n_0}n^4e^{-nx}= e^{-x(n_0-1)}(n_{0}^{4}y + (4 n_0^3 + 6 n_0^{2} + 4 n_0 + 1)y^2 + 2 (6 n_{0}^{2} + 12 n_{0} + 7)y^3 + 12(2n_0+3)y^4 + 24y^5) \\quad (IIa)$\n",
    "\n",
    "where $y=\\frac{e^{-x}}{1-e^{-x}}$ and coded,"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "D4a(2.0, 5) = 0.03892088036985504\n",
      "D4a(1.0e6, 10) = 1.516936780898734357589463782261016246401085581490856436959012937511728895412703e-4342941\n",
      "D4a(1.0e6, 100) = 6.451709692821766008843654827135633239697947425281381499819553501855330737159978e-43429441\n"
     ]
    }
   ],
   "source": [
    "function D4a(x::Real, n0::Int)\n",
    "    y = bigexp(-x)/(1-bigexp(-x))   \n",
    "    return real(bigexp(-x*(n0-1))*y*(n0^4 + y*(4*n0^3+6*n0^2+4*n0+1 + y*(2*(6*n0^2+12*n0+7) + y*(12*(2*n0+3)+24*y)))))\n",
    "end\n",
    "\n",
    "@show D4a(2.0,5) # compare with 0.038920880369855\n",
    "@show D4a(1e6,10)\n",
    "@show D4a(1e6,100)\n",
    "nothing"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Recall the formal bound,\n",
    "\n",
    "$\\sum_{n=n_0}^\\infty (2\\pi^2 n^4 e^{9u} - 3 \\pi n^2 e^{5u}) \\exp(-\\pi n^2 e^{4u}) \\le 2\\pi^2 e^{9u} (\\frac{d^4}{dx^4} \\frac{e^{-n_0x}}{1-e^{-x}})\\big \\vert_{x=\\pi n_0 e^{4u}} -  3 \\pi e^{5u}(\\frac{d^2}{dx^2} \\frac{e^{-n_0x}}{1-e^{-x}})\\big \\vert_{x=\\pi n_0 e^{4u}}$\n",
    "\n",
    "Prototype code can be written directly from this formula using D2a and D4a above. For numerical purposes, though, the exponents of the $e^{5u}$ and $e^{9u}$ factors should be combined with exponents of other factors.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "prototype_bnd(0.001, 100) = 7.101161308744534532065118593045617511828057924710503130101191535229904093022724e-13690\n",
      "prototype_bnd(1.0, 100) = 9.0217832090074763575810909314130918278049185248410179394774153461078611978531e-744912\n",
      "prototype_bnd(1000.0, 100) = BigFloat(NaN, 256)\n"
     ]
    }
   ],
   "source": [
    "function prototype_bnd(u::Real, n0::Int)\n",
    "    x = real(π*n0*bigexp(4*u))\n",
    "    return 2*π^2*exp(9*u)*D4a(x,n0)-3*π*exp(5*u)*D2a(x,n0)\n",
    "end\n",
    "\n",
    "@show prototype_bnd(1e-3,100)\n",
    "@show prototype_bnd(1.0, 100)\n",
    "@show prototype_bnd(1e3, 100)\n",
    "nothing"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The following code should have better numerical behavior and can be checked against the protoype for small values of $u$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Φ_bound(0.001, 100) = 7.101161308731720104634670418457239645131876418538829873140618708843780078804641e-13690\n",
      "Φ_bound(1.0, 100) = 9.021783209007476237195363833140205803536745213228483882660415987178116901096347e-744912\n",
      "Φ_bound(1000.0, 100) = 0.000000000000000000000000000000000000000000000000000000000000000000000000000000\n"
     ]
    }
   ],
   "source": [
    "function Φ_bound(u::Real, n0::Int)\n",
    "    x = real(π*n0*bigexp(4*u))\n",
    "    y = real(bigexp(-x)/(1-bigexp(-x)))\n",
    "    d2term = 3*π*real(bigexp(5*u-x*(n0-1))*y*(n0^2 + y*(2*n0+1 + 2*y)))\n",
    "    d4term = 2*π^2*real(bigexp(9*u-x*(n0-1))*y*(n0^4 + y*(4*n0^3+6*n0^2+4*n0+1 + y*(2*(6*n0^2+12*n0+7) + y*(12*(2*n0+3)+24*y)))))\n",
    "    return d4term - d2term \n",
    "end\n",
    "\n",
    "@show Φ_bound(1e-3,100)\n",
    "@show Φ_bound(1.0, 100)\n",
    "@show Φ_bound(1e3, 100)\n",
    "nothing"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "A quick check that partial sums of the tails do satisfy the bounds follows."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "psum_Φ(0.001, 10, 100) = 2.054420101126833476241936672463715471562462004548019796835598374296172876360351e-132\n",
      "Φ_bound(0.001, 10) = 2.0544201011269086e-132\n",
      "psum_Φ(0.001, 10, 100) ≤ Φ_bound(0.001, 10) = true\n",
      "psum_Φ(1.0, 10, 100) ≤ Φ_bound(1.0, 10) = true\n"
     ]
    }
   ],
   "source": [
    "function psum_Φ(u::Real, n0::Int, n1::Int)\n",
    "    psum = 0.0\n",
    "    for n in n0:n1\n",
    "        x = π*n^2*real(bigexp(4*u))\n",
    "        psum += real(2*π^2*n^4*bigexp(9*u-x)-3*π*n^2*bigexp(5*u-x))\n",
    "    end\n",
    "    return psum\n",
    "end\n",
    "\n",
    "@show psum_Φ(1e-3,10,100)\n",
    "@show Φ_bound(1e-3,10)\n",
    "@show psum_Φ(1e-3,10,100) ≤ Φ_bound(1e-3,10)\n",
    "@show psum_Φ(1.0,10,100) ≤ Φ_bound(1.0,10)\n",
    "nothing"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Errors relative to partial sums."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Φ_bound(0.001, 5) / psum_Φ(0.001, 1, 4) = 1.5511766377580891e-30\n",
      "Φ_bound(0.001, 10) / psum_Φ(0.001, 1, 9) = 4.5993089418689946e-132\n",
      "Φ_bound(1.0, 5) / psum_Φ(1.0, 1, 4) = 9.57956668415956483401075706728480232869201520812488320452283140681368581383168e-1786\n",
      "Φ_bound(10.0, 2) / psum_Φ(10.0, 1, 1) = 2.961017419201487224693499757271271865022786275105162835866098513534449105570907e-963462276345546551\n"
     ]
    }
   ],
   "source": [
    "@show Φ_bound(0.001,5)/psum_Φ(0.001,1,4)\n",
    "@show Φ_bound(0.001,10)/psum_Φ(0.001,1,9)\n",
    "@show Φ_bound(1.0,5)/psum_Φ(1.0,1,4)\n",
    "@show Φ_bound(10.0,2)/psum_Φ(10.0,1,1)\n",
    "nothing"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.2",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
